/*

 About 10k states in the first draft => less than 4k with atomics but 782k (!) states with fairness constraints 

*/

#define idx_unknown -1

#define idx_state_BothOff 0
#define idx_state_LampOff 1
#define idx_state_LightOn 2
#define idx_state_WallOff 3

mtype = { event_ExitState, event_EnterState, event_LampSwitch, event_WallSwitch, event_Dummy, }

typedef event {mtype evId; short toState};

chan  _stateMachineChannel = [6] of {event};
chan  _stateMachineInternalChannel = [6] of {event};
short _currentState = idx_unknown;
bool  _stateMachineReady = false;
bool  _isLightOn = false;


inline send_internal_event(evt, toState) // sorted order
{
    _stateMachineInternalChannel!evt(toState);
}
    
inline send_event(evt, toState)
{
    empty(_stateMachineInternalChannel);
    _stateMachineChannel!evt(toState);
}
    

proctype StateBothOff(chan superChannel; chan eventProcessedChan)
{
    local event evtRecv; 

entry_BothOff:
    /* execute on entry BothOff actions */
    _currentState = idx_state_BothOff; //0
    _stateMachineReady = true;

body_BothOff:
end_BothOff: // valid verification' end
    superChannel?evtRecv; 
    printf("MSC: > StateBothOff event %e in state %d\n", evtRecv.evId, _currentState); 
    atomic {
      if
        :: (evtRecv.evId == event_ExitState && evtRecv.toState == idx_state_BothOff) -> 
        eventProcessedChan!true;
        goto exit_BothOff;
        
        :: (evtRecv.evId == event_LampSwitch) -> 
        /* execute transition actions then on exit state BothOff; not UML-compliant*/
        send_internal_event(event_EnterState, idx_state_WallOff);
        eventProcessedChan!true;
        goto exit_BothOff;

        :: (evtRecv.evId == event_WallSwitch) -> 
        send_internal_event(event_EnterState, idx_state_LampOff);
        eventProcessedChan!true;
        goto exit_BothOff;
        
        // No warning?
        :: (evtRecv.evId == event_Dummy) -> 
        eventProcessedChan!true;
        goto body_BothOff;
        
        :: else -> assert(false); skip;
      fi

      //TODO  send event to substates/regions for processing
      //TODO: attempt to process the event per super's transition table
      goto body_BothOff;
      
    } // atomic
exit_BothOff:
    /* execute on exit BothOff actions */
}


proctype StateLampOff(chan superChannel; chan eventProcessedChan)
{
    local event evtRecv; 

entry_LampOff:
    _currentState = idx_state_LampOff; //1

body_LampOff:
    superChannel?evtRecv; 
    printf("MSC: > StateLampOff event %e in state %d\n", evtRecv.evId, _currentState); 
    atomic {
      if
        :: (evtRecv.evId == event_ExitState && evtRecv.toState == idx_state_LampOff) -> 
        eventProcessedChan!true;
        goto exit_LampOff;
        
        :: (evtRecv.evId == event_LampSwitch) -> 
        send_internal_event(event_EnterState, idx_state_LightOn);
        eventProcessedChan!true;
        goto exit_LampOff;

        :: (evtRecv.evId == event_WallSwitch) -> 
        send_internal_event(event_EnterState, idx_state_BothOff);
        eventProcessedChan!true;
        goto exit_LampOff;

        :: else -> assert(false); skip;
      fi

      //  TODO: send event to substates/regions for processing
      //TODO: attempt to process the event per super's transition table
      goto body_LampOff;
      
    } // atomic

exit_LampOff:
}

proctype StateLightOn(chan superChannel; chan eventProcessedChan)
{
    local event evtRecv; 

entry_LightOn: 
    _currentState = idx_state_LightOn; //2
    _isLightOn = true;

body_LightOn:
    superChannel?evtRecv; 
    printf("MSC: > StateLightOn event %e in state %d\n", evtRecv.evId, _currentState); 
    atomic {
      if
        :: (evtRecv.evId == event_ExitState && evtRecv.toState == idx_state_LightOn) -> 
        eventProcessedChan!true;
        goto exit_LightOn;
        
        :: (evtRecv.evId == event_LampSwitch) -> 
        send_internal_event(event_EnterState, idx_state_LampOff);
        eventProcessedChan!true;
        goto exit_LightOn;

        :: (evtRecv.evId == event_WallSwitch) -> 
        send_internal_event(event_EnterState, idx_state_WallOff);
        eventProcessedChan!true;
        goto exit_LightOn;

        :: else -> assert(false); skip;
      fi

      //  TODO: send event to substates/regions for processing
      //TODO: attempt to process the event per super's transition table
      goto body_LightOn;
      
    } // atomic

exit_LightOn:
    _isLightOn = false;
}

proctype StateWallOff(chan superChannel; chan eventProcessedChan)
{
    local event evtRecv; 

entry_WallOff:
    _currentState = idx_state_WallOff; //3

body_WallOff:
    superChannel?evtRecv; 
    printf("MSC: > StateWallOff event %e in state %d\n", evtRecv.evId, _currentState); 
    atomic {
      if
        :: (evtRecv.evId == event_ExitState && evtRecv.toState == idx_state_WallOff) -> 
        eventProcessedChan!true;
        goto exit_WallOff;
        
        :: (evtRecv.evId == event_LampSwitch) -> 
        send_internal_event(event_EnterState, idx_state_BothOff);
        eventProcessedChan!true;
        goto exit_WallOff;

        :: (evtRecv.evId == event_WallSwitch) -> 
        send_internal_event(event_EnterState, idx_state_LightOn);
        eventProcessedChan!true;
        goto exit_WallOff;

        :: else -> assert(false); skip;
      fi

      //  TODO: send event to substates/regions for processing
      //TODO: attempt to process the event per super's transition table
      goto body_WallOff;
      
    } // atomic

exit_WallOff:
}

proctype Switch(chan superChannel)
{
    local event evtRecv; 
    chan substateChannel = [1] of {event};
    
    chan eventProcessedChannel = [0] of {bool}
    bool eventProcessed = false;

entry_Switch:
    send_internal_event(event_EnterState, idx_state_BothOff); // initial state

body_Switch:
    if
    :: nempty(_stateMachineInternalChannel) -> _stateMachineInternalChannel?evtRecv
    :: empty(_stateMachineInternalChannel)  -> end_Switch: superChannel?evtRecv;
    fi
    printf("MSC: > Switch event %e in state %d\n", evtRecv.evId, _currentState); 
      
    atomic {
      if
        :: (evtRecv.evId == event_EnterState && evtRecv.toState == idx_state_BothOff) -> 
        run StateBothOff(substateChannel, eventProcessedChannel);
        goto body_Switch;

        :: (evtRecv.evId == event_EnterState && evtRecv.toState == idx_state_LampOff) -> 
        run StateLampOff(substateChannel, eventProcessedChannel);
        goto body_Switch;

        :: (evtRecv.evId == event_EnterState && evtRecv.toState == idx_state_LightOn) -> 
        run StateLightOn(substateChannel, eventProcessedChannel);
        goto body_Switch;

        :: (evtRecv.evId == event_EnterState && evtRecv.toState == idx_state_WallOff) -> 
        run StateWallOff(substateChannel, eventProcessedChannel);
        goto body_Switch;

        :: (evtRecv.evId == event_ExitState) -> 
        substateChannel!evtRecv;
        eventProcessedChannel?eventProcessed;
        goto body_Switch;

        :: else -> skip; // send to substates for processing
        //goto body_Switch;
      fi
      
      //send event to substates/regions for processing
      substateChannel!evtRecv.evId(_currentState);
      eventProcessedChannel?eventProcessed;
      if
      :: (eventProcessed == true) -> goto body_Switch;
      :: else -> skip; // to the transition table next
      fi
      
      //TODO: attempt to process the event per super's transition table
      assert(false);
      
      goto body_Switch;
    } // atomic

exit_Switch: 
} // Switch


proctype TestEnvironment() 
{
    (_stateMachineReady == true);
    assert(_currentState == idx_state_BothOff);
    
    if
    :: true -> 
       send_event(event_WallSwitch, _currentState); 
       send_event(event_LampSwitch, _currentState); //assert(_isLightOn == true); // error because race condition
    :: true -> 
       send_event(event_LampSwitch, _currentState); 
       send_event(event_WallSwitch, _currentState); //assert(_isLightOn == true); // idem
    fi
    (_isLightOn == true);
    assert(_currentState == idx_state_LightOn);
    
    if
    :: true -> 
       send_event(event_LampSwitch, _currentState);
       send_event(event_WallSwitch, _currentState);
    :: true -> 
       send_event(event_WallSwitch, _currentState);
       send_event(event_LampSwitch, _currentState);
    fi
    (_isLightOn == false);
    (_currentState == idx_state_BothOff);
} // TestEnvironment


init {
    atomic {
        run Switch(_stateMachineChannel); 
        run TestEnvironment(); 
    }
    //(_nr_pr == 1); 
}


ltl { always/*[]*/ eventually/*<>*/
    (             (_currentState == idx_state_BothOff && _isLightOn == false) 
    implies/*->*/ (_currentState == idx_state_LightOn && _isLightOn == true) 
    implies/*->*/ (_currentState == idx_state_BothOff && _isLightOn == false)
)}

